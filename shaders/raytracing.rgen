#version 460
#extension GL_EXT_ray_tracing : require

layout(set = 1, binding = 0) uniform accelerationStructureEXT tlas;
layout(set = 0, binding = 0) uniform camera_ubo {
    vec3 pos;
    mat4 view;
    mat4 proj;
    mat4 inv_view;
    mat4 inv_proj;
} camera;

layout(set = 1, binding = 2, rgba32f) uniform image2D img;

layout(location = 0) rayPayloadEXT vec4 payload;

void main()
{
    const vec2 pixel = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
    const vec2 d = (pixel/gl_LaunchSizeEXT.xy) * 2.0 - 1.0;

    vec3 origin = camera.pos;
    vec4 target = camera.inv_proj * vec4(d.x, d.y, 1, 1);
    vec4 direction = camera.inv_view * vec4(normalize(target.xyz), 0);

    traceRayEXT(tlas,
        gl_RayFlagsOpaqueEXT, 
        0xff, 
        0,  // sbt offset
        0,  // sbt stride
        0,  // miss index
        origin, 
        0.001,
        direction.xyz, 
        10000.0, 
        0
    );

    imageStore(img, ivec2(gl_LaunchIDEXT.xy), payload);
}
